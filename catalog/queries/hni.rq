PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:hiddenLabel ?hiddenLabel ;
        skos:scopeNote ?scopeNote ;
        skos:exactMatch ?gettyIRI .
    ?uri skos:broader ?broader_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?uri skos:narrower ?narrower_uri .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
}
WHERE {
    ?uri skos:prefLabel ?prefLabel
    OPTIONAL {
        ?uri skos:scopeNote ?scopeNote .
    }
    OPTIONAL {
        ?uri skos:altLabel ?altLabel .
        FILTER(LANG(?altLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:hiddenLabel ?hiddenLabel .
        FILTER(LANG(?hiddenLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:broader ?broader_uri .
        ?broader_uri skos:prefLabel ?broader_prefLabel .
        FILTER(LANG(?broader_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:narrower ?narrower_uri .
        ?narrower_uri skos:prefLabel ?narrower_prefLabel .
        FILTER(LANG(?narrower_prefLabel) = "nl")
    }
    OPTIONAL {
        ?uri skos:exactMatch ?gettyIRI .
        SERVICE <http://vocab.getty.edu/sparql> { 
          ?gettyIRI skos:scopeNote/rdf:value ?scopeNote .
        }
    }
    FILTER(CONTAINS(LCASE(?prefLabel), LCASE(?query)))
    
}
LIMIT 1000
